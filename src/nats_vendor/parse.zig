// Copyright (c) 2025 g41797
// SPDX-License-Identifier: MIT

//! Parsing utilities for NATS protocol messages.
//!
//! Server messages format:
//! - INFO {"option_name":option_value,...}␍␊
//! - PING␍␊
//! - PONG␍␊
//! - +OK␍␊
//! - -ERR <error message>␍␊
//! - MSG <subject> <sid> [reply-to-subject] <#bytes>␍␊[payload]␍␊
//! - HMSG <subject> <sid> [reply-to-subject] <#header bytes> <#total bytes>␍␊[headers]␍␊␍␊[payload]␍␊

/// Counts the number of whitespace-separated substrings in a line.
pub fn count_substrings(line: []const u8) u16 {
    const trl = std.mem.trim(u8, line, " \t\r\n");

    if (trl.len == 0) {
        return 0;
    }

    var subs_it = std.mem.splitAny(u8, trl, " \t");

    var count: u16 = 1;
    _ = subs_it.first();

    while (true) {
        if (subs_it.next()) |*val| {
            if (val.len > 0) {
                count += 1;
            }
        } else {
            return count;
        }
    }
}

/// Extracts the trailing size value from a protocol line.
/// Returns the line without the size and the parsed size value.
pub fn cut_tail_size(line: []const u8) !struct { shrinked: []const u8, size: usize } {
    const subs = try cut_tail(line);

    var size: usize = 0;

    if (std.fmt.parseInt(usize, subs.tail, 10)) |parsed| {
        size = parsed;
    } else |parseErr| {
        return parseErr;
    }

    return .{ .shrinked = subs.shrinked, .size = size };
}

/// Extracts the last whitespace-separated token from a line.
/// Returns the line without the tail and the tail token.
pub fn cut_tail(line: []const u8) !struct { shrinked: []const u8, tail: []const u8 } {
    const trl = std.mem.trim(u8, line, " \t\r\n");

    if (trl.len == 0) {
        return error.EmptyLine;
    }

    var tailIndex: usize = 0;
    if (std.mem.lastIndexOfScalar(u8, trl, ' ')) |val| {
        tailIndex = val;
    } else {
        return error.BadFormat;
    }

    var lenOfTail = trl.len - tailIndex - 1;

    for (line[tailIndex + 1 .. trl.len], 0..) |char, indx| {
        if ((char == '\r') or (char == '\n')) {
            lenOfTail = indx;
            break;
        }
    }

    return .{ .shrinked = trl[0..tailIndex], .tail = trl[tailIndex + 1 .. tailIndex + 1 + lenOfTail] };
}

const Status = std.http.Status;

/// Extracts the error text from a JetStream response.
/// Returns null if no error is found.
pub fn responseErrorText(resp: []const u8) ?[]const u8 {
    const expected = "\"error\":{\"code\":";

    if (std.mem.indexOf(u8, resp, expected)) |index| {
        var subs_it = std.mem.splitAny(u8, resp[index + expected.len ..], ",");

        if (std.fmt.parseInt(usize, subs_it.first(), 10)) |parsed| {
            const status: Status = @enumFromInt(parsed);
            return status.phrase();
        } else |_| {
            return null;
        }
    } else {
        return null;
    }
}

/// Returns true if the response contains an error.
pub fn isFailed(resp: []const u8) bool {
    const errText = responseErrorText(resp);
    if (errText) |_| {
        return true;
    } else {
        return false;
    }
}

/// Extracts the name field from a JetStream response.
/// Returns null if no name is found.
pub fn responseNameText(resp: []const u8) ?[]const u8 {
    const expected = ",\"name\":\"";

    if (std.mem.indexOf(u8, resp, expected)) |index| {
        var subs_it = std.mem.splitAny(u8, resp[index + expected.len ..], "\"");

        const ntext = subs_it.first();

        if (ntext.len == 0) {
            return null;
        }

        return ntext;
    }
    return null;
}

const std = @import("std");
const ascii = std.ascii;
